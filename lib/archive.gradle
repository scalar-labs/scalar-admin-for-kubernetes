apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'scalar-admin-k8s'
            from components.java
            pom {
                name = 'Scalar Admin for Kubernetes'
                description = 'This library provides a fault-tolerant manner to pause Scalar products in Kubernetes environments and ensure the data is transactionally consistent.'
                url = 'https://github.com/scalar-labs/scalar-admin'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'hiroyuki'
                        name = 'Hiroyuki Yamada'
                        email = 'hiroyuki.yamada@scalar-labs.com'
                    }
                    developer {
                        id = 'supl'
                        name = 'Plenty Su'
                        email = 'plenty.su@scalar-labs.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/scalar-labs/scalar-admin-k8s.git'
                    developerConnection = 'scm:git:https://github.com/scalar-labs/scalar-admin-k8s.git'
                    url = 'https://github.com/scalar-labs/scalar-admin-k8s'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            def username = project.findProperty('ossrhUsername') ?: ''
            def password = project.findProperty('ossrhPassword') ?: ''
            credentials {
                username = "${username}"
                password = "${password}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
